buildscript {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply from: "$rootDir/config.gradle"
    apply plugin: "maven-publish"

    ext.describeVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    repositories {
        mavenCentral()
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = publishConfig.gitHubPackagesName
                    url = publishConfig.gitHubPackagesUrl
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            }
            publications {
                gpr(MavenPublication) {
                    from components.java

                    groupId = publishConfig.groupId
                    artifactId = project.name
                    version = publishConfig.version
                }
            }
        }
    }
}
